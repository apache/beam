/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * License); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an AS IS BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Define common lifecycle tasks and artifact types
apply plugin: "base"

task cleanWebsite(type: Delete) {
  delete '.sass-cache', 'vendor', '.testcontent', 'content'
}
clean.dependsOn cleanWebsite

task installGems(type: Exec) {
  mustRunAfter cleanWebsite
  inputs.file 'Gemfile.lock'
  outputs.dir 'vendor'
  commandLine 'bundle', 'install', '--deployment'
}

task buildWebsite(type: Exec) {
  dependsOn installGems
  mustRunAfter cleanWebsite, setupJenkins
  inputs.files 'Gemfile.lock', '_config.yml'
  inputs.dir 'src'
  outputs.dir '.sass-cache'
  outputs.dir 'content'
  outputs.dir '.testcontent'
  commandLine 'bundle', 'exec', 'jekyll', 'build',
    '--config', '_config.yml',
    '--incremental'
}
build.dependsOn buildWebsite

task testWebsite(type: Exec) {
  dependsOn buildWebsite
  inputs.files 'Gemfile.lock', 'Rakefile'
  inputs.dir '.testcontent'
  commandLine 'bundle', 'exec', 'rake', 'test'
}
check.dependsOn testWebsite

task preCommit {
  dependsOn testWebsite
}
