/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * License); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an AS IS BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: project(":").file("build_rules.gradle")
applyJavaNature()

description = "Apache Beam :: Runners :: Direct Java"

/*
 * We need to rely on manually specifying these evaluationDependsOn to ensure that
 * the following projects are evaluated before we evaluate this project. This is because
 * we are attempting to reference the "sourceSets.test.output" directly.
 * TODO: Swap to generating test artifacts which we can then rely on instead of
 * the test outputs directly.
 */
evaluationDependsOn(":beam-runners-parent:beam-runners-core-java")
evaluationDependsOn(":beam-model-parent:beam-model-fn-execution")

dependencies {
  compile library.java.guava
  compile project(path: ":beam-model-parent:beam-model-pipeline", configuration: "shadow")
  compile project(path: ":beam-runners-parent:beam-runners-core-construction-java", configuration: "shadow")
  compile project(path: ":beam-runners-parent:beam-runners-core-java", configuration: "shadow")
  compile project(path: ":beam-runners-parent:beam-runners-local-java-core", configuration: "shadow")
  shadow project(path: ":beam-sdks-parent:beam-sdks-java-parent:beam-sdks-java-core", configuration: "shadow")
  shadow library.java.joda_time
  shadow library.java.findbugs_jsr305
  shadow library.java.slf4j_api
  shadow library.java.hamcrest_core
  shadow library.java.junit
  testCompile project(path: ":beam-sdks-parent:beam-sdks-java-parent:beam-sdks-java-core", configuration: "shadowTest")
  testCompile project(":beam-runners-parent:beam-runners-core-java").sourceSets.test.output
  testCompile project(":beam-model-parent:beam-model-fn-execution").sourceSets.test.output
  testCompile library.java.guava_testlib
  testCompile library.java.slf4j_jdk14
  testCompile library.java.mockito_core
  testCompile library.java.stax2_api
  testCompile library.java.woodstox_core_asl
  testCompile library.java.google_cloud_dataflow_java_proto_library_all
  testCompile library.java.jackson_dataformat_yaml
}

shadowJar {
  dependencies {
    include(dependency(library.java.protobuf_java))
    include(dependency(library.java.protobuf_java_util))
    include(project(path: ":beam-model-parent:beam-model-pipeline", configuration: "shadow"))
    include(project(path: ":beam-runners-parent:beam-runners-core-construction-java", configuration: "shadow"))
    include(project(path: ":beam-runners-parent:beam-runners-core-java", configuration: "shadow"))
    include(project(path: ":beam-runners-parent:beam-runners-local-java-core", configuration: "shadow"))
  }
  relocate "org.apache.beam.runners.core", "org.apache.beam." + project.name.replace("-", ".") + ".repackaged.runners.core"
  relocate "org.apache.beam.model", "org.apache.beam." + project.name.replace("-", ".") + ".repackaged.model"
  relocate "com.google.protobuf", "org.apache.beam." + project.name.replace("-", ".") + ".repackaged.com.google.protobuf"
  relocate "javax.annotation", "org.apache.beam." + project.name.replace("-", ".") + ".repackaged.javax.annotation"
}

task packageTests(type: Jar) {
  from sourceSets.test.output
  classifier = "tests"
}

artifacts.archives packageTests
