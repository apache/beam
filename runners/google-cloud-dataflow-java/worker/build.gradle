/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/******************************************************************************/
// Apply BeamModulePlugin

// Reuse project_root/buildSrc in this build.gradle file to reduce the
// maintenance burden and simpily this file. See BeamModulePlugin for
// documentation on default build tasks and properties that are enabled in
// addition to natures that will be applied to worker.
apply plugin: org.apache.beam.gradle.BeamModulePlugin

group = "org.apache.beam.runners.dataflow"
version = "google-built"

/******************************************************************************/
// Apply Java nature with customized configurations

// Set a specific version of 'com.google.apis:google-api-services-dataflow'
// by adding -Pdataflow.version=<version> in Gradle command. Otherwise,
// 'google_clients_version' defined in BeamModulePlugin will be used as default.
def DATAFLOW_VERSION = "dataflow.version"

// To build FnAPI or legacy worker.
// Use -PisLegacyWorker in Gradle command if build legacy worker, otherwise,
// FnAPI worker is considered as default.
def is_legacy_worker = {
  return project.hasProperty("isLegacyWorker")
}

// Get full dependency of 'com.google.apis:google-api-services-dataflow'
def google_api_services_dataflow = project.hasProperty(DATAFLOW_VERSION) ? "com.google.apis:google-api-services-dataflow:" + getProperty(DATAFLOW_VERSION) : library.java.google_api_services_dataflow

// Returns a string representing the relocated path to be used with the shadow
// plugin when given a suffix such as "com.".
def getWorkerRelocatedPath = { String suffix ->
  return ("org.apache.beam.runners.dataflow.worker.repackaged."
          + suffix)
}

// Following listed dependencies will be shaded only in fnapi worker, not legacy
// worker
def sdk_provided_dependencies = [
  "org.apache.beam:beam-runners-google-cloud-dataflow-java:$beamVersion",
  "org.apache.beam:beam-sdks-java-core:$beamVersion",
  "org.apache.beam:beam-sdks-java-extensions-google-cloud-platform-core:$beamVersion",
  "org.apache.beam:beam-sdks-java-io-google-cloud-platform:$beamVersion",
  google_api_services_dataflow,
  library.java.avro,
  library.java.google_api_client,
  library.java.google_http_client,
  library.java.google_http_client_jackson,
  library.java.jackson_annotations,
  library.java.jackson_core,
  library.java.jackson_databind,
  library.java.joda_time,
]

// Exclude unneeded dependencies when building jar
def excluded_dependencies = [
  "com.google.auto.service:auto-service",      // Provided scope added from applyJavaNature
  "com.google.auto.value:auto-value",          // Provided scope added from applyJavaNature
  "org.codehaus.jackson:jackson-core-asl",     // Exclude an old version of jackson-core-asl introduced by google-http-client-jackson
  "org.objenesis:objenesis",                   // Transitive dependency introduced from Beam
  "org.tukaani:xz",                            // Transitive dependency introduced from Beam
  library.java.commons_compress,               // Transitive dependency introduced from Beam
  library.java.error_prone_annotations,        // Provided scope added in worker
  library.java.hamcrest_core,                  // Test only
  library.java.hamcrest_library,               // Test only
  library.java.junit,                          // Test only
]

applyJavaNature(validateShadowJar: false, shadowClosure: DEFAULT_SHADOW_CLOSURE << {
  dependencies {
    include(project(path: ":beam-runners-google-cloud-dataflow-java-windmill", configuration: "shadow"))
    include(dependency(".*:.*"))

    if (is_legacy_worker()) {
      sdk_provided_dependencies.each {
        exclude(dependency(it))
      }
      excluded_dependencies.each {
        exclude(dependency(it))
      }
    }
  }

  // Archive name pattern: ${name}-${appendix}-${version}-${classifier}.jar
  appendix = is_legacy_worker() ? "legacy-bundled" : "fnapi-bundled"

  // Include original source files extracted under
  // '$buildDir/original_sources_to_package' to jar
  from "$buildDir/original_sources_to_package"

  exclude "META-INF/LICENSE.txt"
  exclude "about.html"
  exclude "google/protobuf/*.proto"
  exclude "windmill*.proto"

  if (is_legacy_worker()) {
    relocate("com.", getWorkerRelocatedPath("com.")) {
      exclude "com.fasterxml.jackson.**"
      exclude "com.google.api.client.**"
      exclude "com.google.api.services.bigquery.**"
      exclude "com.google.api.services.clouddebugger.**"
      exclude "com.google.api.services.dataflow.**"
      exclude "com.google.api.services.datastore.**"
      exclude "com.google.api.services.pubsub.**"
      exclude "com.google.api.services.storage.**"
      exclude "com.google.auth.**"
      exclude "com.google.cloud.dataflow.**"
      exclude "com.sun.management*"
      exclude "com.sun.management.**"
    }
    relocate("javax.servlet", getWorkerRelocatedPath("javax.servlet"))
    relocate("io.", getWorkerRelocatedPath("io."))
    relocate("okio.", getWorkerRelocatedPath("okio."))
    relocate("org.", getWorkerRelocatedPath("org.")) {
      // Exclude netty-tcnative from shading since gRPC relies on Netty to be able
      // to load org.apache.tomcat.jni.SSL to provide an SSL context.
      exclude "org.apache.avro.**"
      exclude "org.apache.beam.**"
      exclude "org.apache.tomcat.jni.**"
      exclude "org.conscrypt.**"
      exclude "org.eclipse.jetty.alpn.**"
      exclude "org.eclipse.jetty.npn.**"
      exclude "org.hamcrest.**"
      exclude "org.joda.time.**"
      exclude "org.junit.**"
      exclude "org.slf4j.**"
      exclude "org.w3c.dom.**"
    }
    relocate("org.apache.beam.runners.core.construction.",
             getWorkerRelocatedPath("org."))
  }
})

/******************************************************************************/
// Define the set of repositories and dependencies required to
// fetch and enable plugins.

buildscript {
  repositories {
    maven { url offlineRepositoryRoot }
    mavenLocal()
  }

  dependencies {
    classpath 'net.researchgate:gradle-release:2.6.0'                                                   // Enable gradle-based release management
    classpath "net.ltgt.gradle:gradle-apt-plugin:0.13"                                                  // Enable a Java annotation processor
    classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.5"                                        // Enable proto code generation
    classpath "io.spring.gradle:propdeps-plugin:0.0.9.RELEASE"                                          // Enable provided and optional configurations
    classpath "gradle.plugin.org.nosphere.apache:creadur-rat-gradle:0.3.1"                              // Enable Apache license enforcement
    classpath "com.commercehub.gradle.plugin:gradle-avro-plugin:0.11.0"                                 // Enable Avro code generation
    classpath "com.diffplug.spotless:spotless-plugin-gradle:3.6.0"                                      // Enable a code formatting plugin
    classpath "gradle.plugin.com.github.blindpirate:gogradle:0.10"                                      // Enable Go code compilation
    classpath "gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.13.0"                           // Enable building Docker containers
    classpath "cz.malohlava:visteg:1.0.3"                                                               // Enable generating Gradle task dependencies as ".dot" files
    classpath "com.github.jengelman.gradle.plugins:shadow:2.0.4"                                        // Enable shading Java dependencies
    classpath "ca.coglinc:javacc-gradle-plugin:2.4.0"                                                   // Enable the JavaCC parser generator
    classpath "gradle.plugin.io.pry.gradle.offline_dependencies:gradle-offline-dependencies-plugin:0.3" // Enable creating an offline repository
    classpath "net.ltgt.gradle:gradle-errorprone-plugin:0.0.13"                                         // Enable errorprone Java static analysis
    classpath "com.github.ben-manes:gradle-versions-plugin:0.17.0"                                      // Enable dependency checks

    // Plugins which require online access should not be enabled when running in offline mode.
    if (!gradle.startParameter.isOffline()) {
      classpath "com.gradle:build-scan-plugin:1.13.1"                                                   // Enable publishing build scans
    }
  }
}

/******************************************************************************/
// Configure the worker root project

configurations {
  sourceFile

  // Ban these dependencies from all configurations
  all {
    // Ban the usage of AppleJavaExtensions in findbugs since it can't be
    // imported to google3 due to license issue.
    exclude group: "com.apple", module: "AppleJavaExtensions"
  }
}

dependencies {
  // Set dependencies to shadow scope by default, but in a property so they can
  // be downgraded when building a legacy (non-FnAPI) worker.
  if (is_legacy_worker()) {
    sdk_provided_dependencies.each {
      provided(it)
    }
  } else {
    sdk_provided_dependencies.each {
      shadow(it)
    }
  }

  compile "org.apache.beam:beam-model-fn-execution:$beamVersion"
  compile "org.apache.beam:beam-model-pipeline:$beamVersion"
  compile "org.apache.beam:beam-runners-core-construction-java:$beamVersion"
  compile "org.apache.beam:beam-runners-core-java:$beamVersion"
  compile "org.apache.beam:beam-runners-java-fn-execution:$beamVersion"
  compile "org.apache.beam:beam-sdks-java-fn-execution:$beamVersion"
  compile project(path: "beam-runners-google-cloud-dataflow-java-windmill", configuration: "shadow")

  compile library.java.guava
  compile library.java.slf4j_api
  compile "javax.servlet:javax.servlet-api:3.1.0"
  compile "org.conscrypt:conscrypt-openjdk:1.1.3:linux-x86_64"
  compile "org.eclipse.jetty:jetty-server:9.2.10.v20150310"
  compile "org.eclipse.jetty:jetty-servlet:9.2.10.v20150310"

  provided library.java.error_prone_annotations

  runtime library.java.slf4j_jdk14

  sourceFile "javax.servlet:javax.servlet-api:3.1.0:sources"

  testCompile "org.apache.beam:beam-runners-core-java:$beamVersion:tests"
  testCompile "org.apache.beam:beam-runners-direct-java:$beamVersion"
  testCompile "org.apache.beam:beam-sdks-java-core:$beamVersion:tests"
  testCompile "org.apache.beam:beam-sdks-java-extensions-google-cloud-platform-core:$beamVersion:tests"

  testCompile library.java.guava_testlib
  testCompile library.java.hamcrest_core
  testCompile library.java.hamcrest_library
  testCompile library.java.junit
  testCompile library.java.mockito_core
}

/******************************************************************************/
// Extract required source files under '$buildDir/original_sources_to_package',
// which will be packed into jar file.

task extractSourceFiles(type: Copy) {
  configurations.sourceFile.files.each {
    if (it.name.contains("javax.servlet-api")) {
      from zipTree(it)
      into "$buildDir/original_sources_to_package/sources/javax.servlet-api"
    }
  }
}
shadowJar.dependsOn extractSourceFiles

/******************************************************************************/
// Override Gradle plugin configurations with correct file path.

tasks.withType(Checkstyle) {
  configFile file("build-tools/src/main/resources/beam/checkstyle.xml")
  configProperties = ["checkstyle.suppressions.file": file("build-tools/src/main/resources/beam/suppressions.xml")]
}

findbugs {
  excludeFilter = file('build-tools/src/main/resources/beam/findbugs-filter.xml')
}

// Disable strict doclint tool in Javadoc8
tasks.withType(Javadoc) {
    options.addStringOption("Xdoclint:none", "-quiet")
}
