/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * License); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an AS IS BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import groovy.json.JsonOutput

apply plugin: org.apache.beam.gradle.BeamModulePlugin
applyJavaNature(publish: false)
// Evaluate the given project before this one, to allow referencing
// its sourceSets.test.output directly.
def evaluationDependsOnList = [
  ":beam-examples-java",
  ":beam-runners-google-cloud-dataflow-java-legacy-worker",
  ":beam-runners-google-cloud-dataflow-java-fn-api-worker",
  ":beam-sdks-java-container",
]

evaluationDependsOnList.each {
  evaluationDependsOn it
}

configurations { dataflowRunnerPreCommit }

dependencies {
  testRuntimeOnly project(path: ":beam-examples-java", configuration: "shadow")
  testRuntimeOnly project(path: ":beam-examples-java", configuration: "shadowTest")
  testRuntimeOnly project(path: ":beam-runners-google-cloud-dataflow-java", configuration: "shadow")
}

def gcpProject = project.findProperty('gcpProject') ?: 'apache-beam-testing'
def gcsTempRoot = project.findProperty('gcsTempRoot') ?: 'gs://temp-storage-for-end-to-end-tests/'
def dockerImageRoot = project.findProperty('dockerImageRoot')?: "us.gcr.io/${gcpProject}/java-examples-it"
def dockerImageContainer = "${dockerImageRoot}/java"
def dockerTag = new Date().format('yyyyMMddHHmmss')
def dockerImageName = "${dockerImageContainer}:${dockerTag}"
def defaultDockerImageName = containerImageName(name: "java")

task preCommitLegacyWorker(type: Test) {
  dependsOn ":beam-runners-google-cloud-dataflow-java-legacy-worker:shadowJar"
  def dataflowWorkerJar = project.findProperty('dataflowWorkerJar') ?: project(":beam-runners-google-cloud-dataflow-java-legacy-worker").shadowJar.archivePath

  //Set workerHarnessContainerImage to empty to make Dataflow pick up the non-versioned container
  //image, which handles a staged worker jar.
  def preCommitBeamTestPipelineOptions = [
     "--project=${gcpProject}",
     "--tempRoot=${gcsTempRoot}",
     "--runner=TestDataflowRunner",
     "--dataflowWorkerJar=${dataflowWorkerJar}",
     "--workerHarnessContainerImage=",
  ]
  testClassesDirs = files(project(":beam-examples-java").sourceSets.test.output.classesDirs)
  include "**/WordCountIT.class"
  include "**/WindowedWordCountIT.class"
  forkEvery 1
  maxParallelForks 4
  systemProperty "beamTestPipelineOptions", JsonOutput.toJson(preCommitBeamTestPipelineOptions)
}

task buildAndPushDockerContainer() {
  dependsOn ":beam-sdks-java-container:docker"
  doLast {
    exec {
      commandLine "docker", "tag", "${defaultDockerImageName}", "${dockerImageName}"
    }
    exec {
      commandLine "gcloud", "docker", "--", "push", "${dockerImageContainer}"
    }
  }
}

task preCommitFnApiWorker(type: Test) {
  dependsOn ":beam-runners-google-cloud-dataflow-java-fn-api-worker:shadowJar"
  dependsOn buildAndPushDockerContainer

  def dataflowWorkerJar = project.findProperty('dataflowWorkerJar') ?: project(":beam-runners-google-cloud-dataflow-java-fn-api-worker").shadowJar.archivePath
  def preCommitBeamTestPipelineOptions = [
          "--project=${gcpProject}",
          "--tempRoot=${gcsTempRoot}",
          "--runner=TestDataflowRunner",
          "--dataflowWorkerJar=${dataflowWorkerJar}",
          "--workerHarnessContainerImage=${dockerImageName}",
          "--experiments=beam_fn_api",
  ]
  testClassesDirs = files(project(":beam-examples-java").sourceSets.test.output.classesDirs)
  include "**/WordCountIT.class"
  include "**/WindowedWordCountIT.class"
  forkEvery 1
  maxParallelForks 4
  systemProperty "beamTestPipelineOptions", JsonOutput.toJson(preCommitBeamTestPipelineOptions)
  useJUnit {
    excludeCategories 'org.apache.beam.sdk.testing.StreamingIT'
  }
}

task preCommit () {
  dependsOn preCommitLegacyWorker
}

task preCommitFnApi () {
  dependsOn preCommitFnApiWorker
  // Clean up built images
  doLast {
    exec {
      commandLine "docker", "rmi", "${dockerImageName}"
    }
    exec {
      commandLine "gcloud", "--quiet", "container", "images", "delete", "${dockerImageName}"
    }
  }
}
