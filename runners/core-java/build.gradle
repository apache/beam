/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * License); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an AS IS BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: project(":").file("build_rules.gradle")
applyJavaNature()

description = "Apache Beam :: Runners :: Core Java"

/*
 * We need to rely on manually specifying these evaluationDependsOn to ensure that
 * the following projects are evaluated before we evaluate this project. This is because
 * we are attempting to reference the "sourceSets.test.output" directly.
 * TODO: Swap to generating test artifacts which we can then rely on instead of
 * the test outputs directly.
 */
evaluationDependsOn(":beam-sdks-parent:beam-sdks-java-parent:beam-sdks-java-core")

// Exclude tests that need a runner
test {
  systemProperty "beamUseDummyRunner", "true"
  useJUnit {
    excludeCategories "org.apache.beam.sdk.testing.NeedsRunner"
  }
}

dependencies {
  compile project(":beam-model-parent:beam-model-pipeline")
  compile project(":beam-sdks-parent:beam-sdks-java-parent:beam-sdks-java-core")
  compile project(":beam-model-parent:beam-model-fn-execution")
  compile project(":beam-runners-parent:beam-runners-core-construction-java")
  compile library.java.findbugs_jsr305
  compile library.java.guava
  compile library.java.grpc_core
  compile library.java.grpc_stub
  compile library.java.joda_time
  compile library.java.slf4j_api
  testCompile project(":beam-sdks-parent:beam-sdks-java-parent:beam-sdks-java-core").sourceSets.test.output
  testCompile library.java.hamcrest_all
  testCompile library.java.mockito_all
  testCompile library.java.junit
  testCompile library.java.slf4j_jdk14
  testCompile library.java.jackson_dataformat_yaml
}

task packageTests(type: Jar) {
  from sourceSets.test.output
  classifier = "tests"
}

artifacts.archives packageTests
