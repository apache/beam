/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * License); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an AS IS BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import groovy.json.JsonOutput

apply from: project(":").file("build_rules.gradle")
applyJavaNature()

description = "Apache Beam :: Runners :: Gearpump"

evaluationDependsOn(":beam-sdks-java-core")

def gearpump_version = "0.8.4"

configurations {
  validatesRunner
}

dependencies {
  compile library.java.guava
  compileOnly "com.typesafe:config:1.3.0"
  compileOnly "org.scala-lang:scala-library:2.11.8"
  shadow project(path: ":beam-sdks-java-core", configuration: "shadow")
  shadow project(path: ":beam-runners-core-java", configuration: "shadow")
  shadow project(path: ":beam-runners-core-construction-java", configuration: "shadow")
  shadow "org.apache.gearpump:gearpump-core_2.11:$gearpump_version"
  shadow "org.apache.gearpump:gearpump-streaming_2.11:$gearpump_version"
  shadow library.java.joda_time
  shadow library.java.jackson_annotations
  shadow library.java.findbugs_jsr305
  shadowTest project(path: ":beam-sdks-java-core", configuration: "shadowTest")
  shadowTest library.java.junit
  shadowTest library.java.hamcrest_core
  shadowTest library.java.jackson_databind
  shadowTest library.java.jackson_dataformat_yaml
  shadowTest library.java.mockito_core
  validatesRunner project(path: ":beam-sdks-java-core", configuration: "shadowTest")
  validatesRunner project(path: project.path, configuration: "shadow")
}

task validatesRunnerStreaming(type: Test) {
  group = "Verification"
  systemProperty "beamTestPipelineOptions", JsonOutput.toJson([
          "--runner=TestGearpumpRunner",
          "--streaming=true",
  ])

  classpath = configurations.validatesRunner
  testClassesDirs = files(project(":beam-sdks-java-core").sourceSets.test.output.classesDirs)
  useJUnit {
    includeCategories 'org.apache.beam.sdk.testing.ValidatesRunner'
    excludeCategories 'org.apache.beam.sdk.testing.FlattenWithHeterogeneousCoders'
    excludeCategories 'org.apache.beam.sdk.testing.UsesStatefulParDo'
    excludeCategories 'org.apache.beam.sdk.testing.UsesTimersInParDo'
    excludeCategories 'org.apache.beam.sdk.testing.UsesSplittableParDo'
    excludeCategories 'org.apache.beam.sdk.testing.UsesAttemptedMetrics'
    excludeCategories 'org.apache.beam.sdk.testing.UsesCommittedMetrics'
    excludeCategories 'org.apache.beam.sdk.testing.UsesTestStream'
    excludeCategories 'org.apache.beam.sdk.testing.UsesCustomWindowMerging'
    excludeCategories 'org.apache.beam.sdk.testing.UsesParDoLifecycle'
  }
}

task validatesRunner {
  group = "Verification"
  description "Validates Gearpump runner"
  dependsOn validatesRunnerStreaming
}
