/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * License); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an AS IS BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: project(":").file("build_rules.gradle")
applyJavaNature()

description = "Apache Beam :: Runners :: Apex"

/*
 * We need to rely on manually specifying these evaluationDependsOn to ensure that 
 * the following projects are evaluated before we evaluate this project. This is because
 * we are attempting to reference the "sourceSets.test.output" directly.       
 * TODO: Swap to generating test artifacts which we can then rely on instead of 
 * the test outputs directly.
 */
evaluationDependsOn(":beam-sdks-parent:beam-sdks-java-parent:beam-sdks-java-core")
evaluationDependsOn(":beam-model-parent:beam-model-fn-execution")
evaluationDependsOn(":beam-runners-parent:beam-runners-core-java")

dependencies {
  compile project(":beam-model-parent:beam-model-pipeline")
  compile project(":beam-sdks-parent:beam-sdks-java-parent:beam-sdks-java-core")
  compile project(":beam-runners-parent:beam-runners-core-construction-java")
  compile project(":beam-runners-parent:beam-runners-core-java")
  compile library.java.apex_common
  compile library.java.malhar_library
  compile library.java.apex_engine
  compile library.java.commons_lang3
  compile library.java.findbugs_jsr305
  runtime library.java.apex_engine
  testCompile project(":beam-sdks-parent:beam-sdks-java-parent:beam-sdks-java-core").sourceSets.test.output
  testCompile project(":beam-model-parent:beam-model-fn-execution").sourceSets.test.output
  testCompile project(":beam-runners-parent:beam-runners-core-java").sourceSets.test.output
  testCompile library.java.hamcrest_core
  testCompile library.java.junit
  testCompile library.java.mockito_core
  testCompile library.java.jackson_dataformat_yaml
}

// TODO: Update this so that the generated file is added to the explicitly added instead of
// just outputting the file in the correct path.
task buildDependencyTree(type: DependencyReportTask) {
  configurations = [project.configurations.runtimeClasspath]
  outputFile = new File(buildDir, "classes/java/main/org/apache/beam/runners/apex/dependency-tree")
  // TODO: Migrate ApexYarnLauncher to use the Gradles dependency tree output instead of Mavens
  // so we don't have to try to replace the format of the file on the fly

  // TODO: Fix the regular expression replacement
  doLast {
    ant.replaceregexp(file: outputFile, match: "org.apache.hadoop[^:]*:[^:]*", replace: "\0:jar", flags: "g")
  }
}
compileJava.dependsOn buildDependencyTree

task packageTests(type: Jar) {
  from sourceSets.test.output
  classifier = "tests"
}

artifacts.archives packageTests
