/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * License); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an AS IS BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins { id 'org.apache.beam.module' }
applyJavaNature( automaticModuleName: 'org.apache.beam.sdk.io.hbase')
provideIntegrationTestingDependencies()
enableJavaPerformanceTesting()


description = "Apache Beam :: SDKs :: Java :: IO :: HBase"
ext.summary = "Library to read and write from/to HBase"

test {
  systemProperty "log4j.configuration", "log4j-test.properties"
  jvmArgs "-XX:-UseGCOverheadLimit"
  if (System.getProperty("beamSurefireArgline")) {
    jvmArgs System.getProperty("beamSurefireArgline")
  }
  jvmArgs "-Dtest.build.data.basedirectory=build/test-data"
}

def hbase_version = "2.6.1-hadoop3"

dependencies {
  implementation library.java.vendored_guava_32_1_2_jre
  implementation project(path: ":sdks:java:core", configuration: "shadow")
  implementation project(":sdks:java:io:hadoop-common")
  implementation library.java.slf4j_api
  provided "org.apache.hbase:hbase-shaded-client:$hbase_version"
  testImplementation project(path: ":sdks:java:io:common")
  testImplementation project(path: ":sdks:java:core", configuration: "shadowTest")
  testImplementation library.java.junit
  testImplementation library.java.hamcrest
  // shaded-testing-utils has shaded all Hadoop/HBase dependencies
  testImplementation("org.apache.hbase:hbase-shaded-testing-util:$hbase_version")
  testRuntimeOnly project(path: ":runners:direct-java", configuration: "shadow")
}
