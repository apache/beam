/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * License); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an AS IS BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: org.apache.beam.gradle.BeamModulePlugin
applyJavaNature(shadowClosure: {
    dependencies {
        include(dependency(project.library.java.guava))
    }
    // guava uses the com.google.common and com.google.thirdparty package namespaces
    relocate("com.google.common", project.getJavaRelocatedPath("com.google.common")) {
        // com.google.common is too generic, need to exclude guava-testlib
        exclude "com.google.common.collect.testing.**"
        exclude "com.google.common.escape.testing.**"
        exclude "com.google.common.testing.**"
        exclude "com.google.common.util.concurrent.testing.**"
        // don't relocate because the cassandra driver's public API uses it
        exclude "com.google.common.util.concurrent.ListenableFuture"
    }
    // we haven't relocated this so we shouldn't shadow it either
    exclude "com/google/common/util/concurrent/ListenableFuture.class"
    relocate "com.google.thirdparty", project.getJavaRelocatedPath("com.google.thirdparty")
})
provideIntegrationTestingDependencies()
enableJavaPerformanceTesting()

description = "Apache Beam :: SDKs :: Java :: IO :: Cassandra"
ext.summary = "IO to read and write with Apache Cassandra database"

def cassandra_version = "3.5.0"

dependencies {
  compile library.java.guava
  shadow project(path: ":beam-sdks-java-core", configuration: "shadow")
  shadow library.java.slf4j_api
  shadow "com.datastax.cassandra:cassandra-driver-core:$cassandra_version"
  shadow "com.datastax.cassandra:cassandra-driver-mapping:$cassandra_version"
  testCompile project(path: ":beam-runners-direct-java", configuration: "shadow")
  testCompile project(path: ":beam-sdks-java-io-common", configuration: "shadowTest")
  testCompile library.java.junit
  testCompile library.java.hamcrest_core
  testCompile library.java.hamcrest_library
  testCompile library.java.slf4j_jdk14
  testCompile library.java.mockito_core
}
