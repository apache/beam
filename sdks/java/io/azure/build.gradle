import groovy.json.JsonOutput

/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * License); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an AS IS BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'org.apache.beam.module'
}

applyJavaNature(
  automaticModuleName: 'org.apache.beam.sdk.io.azure',
  enableChecker: false)

description = "Apache Beam :: SDKs :: Java :: IO :: Azure"
ext.summary = "IO library to read and write Azure services from Beam."

repositories { jcenter() }

dependencies {
  compile library.java.vendored_guava_26_0_jre
  compile project(path: ":sdks:java:core", configuration: "shadow")
  compile "com.azure:azure-storage-blob:12.1.0"
  compile "com.azure:azure-identity:1.0.8"
  compile "com.microsoft.azure:azure-storage:8.0.0"
  compile "commons-io:commons-io:2.6"
  compile library.java.slf4j_api
  testCompile project(path: ":sdks:java:core", configuration: "shadowTest")
  testCompile library.java.mockito_core
  testCompile library.java.junit
  testRuntimeOnly library.java.slf4j_jdk14
}

/**
 * These are integration tests with the real Azure service and the DirectRunner.
 */
task integrationTest(type: Test, dependsOn: processTestResources) {
  group = "Verification"
  def gcpProject = project.findProperty('gcpProject') ?: 'apache-beam-testing'
  def gcpTempRoot = project.findProperty('gcpTempRoot') ?: 'gs://temp-storage-for-end-to-end-tests'
  systemProperty "beamTestPipelineOptions", JsonOutput.toJson([
          "--runner=DirectRunner",
          "--project=${gcpProject}",
          "--tempRoot=${gcpTempRoot}",
          "--inputFile=azfs://ettasaccount/test-container/test-blob"
  ])

  // Disable Gradle cache: these ITs interact with live service that should always be considered "out of date"
  outputs.upToDateWhen { false }

  include '**/WordCountIT.class'
  maxParallelForks 4
  classpath = sourceSets.test.runtimeClasspath
  testClassesDirs = sourceSets.test.output.classesDirs

  useJUnit {
    excludeCategories "org.apache.beam.sdk.testing.UsesKms"
  }
}
