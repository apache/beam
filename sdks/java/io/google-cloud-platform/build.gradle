/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * License); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an AS IS BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import groovy.json.JsonOutput

plugins { id 'org.apache.beam.module' }
applyJavaNature(
  enableChecker: false,
  automaticModuleName: 'org.apache.beam.sdk.io.gcp',
  enableSpotbugs: false,
)

description = "Apache Beam :: SDKs :: Java :: IO :: Google Cloud Platform"
ext.summary = "IO library to read and write Google Cloud Platform systems from Beam."

dependencies {
  implementation project(path: ":sdks:java:core", configuration: "shadow")
  implementation project(":sdks:java:expansion-service")
  implementation project(":sdks:java:extensions:google-cloud-platform-core")
  implementation project(":sdks:java:extensions:protobuf")
  implementation library.java.avro
  implementation library.java.bigdataoss_util
  implementation library.java.gax
  implementation library.java.gax_grpc
  implementation library.java.google_api_client
  implementation library.java.google_api_services_bigquery
  implementation library.java.google_api_services_healthcare
  implementation library.java.google_api_services_pubsub
  implementation library.java.google_auth_library_credentials
  implementation library.java.google_auth_library_oauth2_http
  implementation library.java.google_cloud_bigquery_storage
  implementation(library.java.google_cloud_bigtable_client_core) {
    exclude group: 'io.grpc', module: 'grpc-core' // Use Beam's version
    exclude group: 'io.grpc', module: 'grpc-grpclb'
  }
  implementation library.java.google_cloud_core
  implementation(library.java.google_cloud_core_grpc) {
    exclude group: 'io.grpc', module: 'grpc-core' // Use Beam's version
  }
  implementation library.java.google_cloud_datastore_v1_proto_client
  implementation library.java.google_cloud_pubsublite
  implementation library.java.google_cloud_spanner
  implementation library.java.google_http_client
  implementation library.java.google_http_client_jackson2
  implementation library.java.grpc_alts
  implementation library.java.grpc_auth
  implementation library.java.grpc_core
  implementation library.java.grpc_context
  implementation library.java.grpc_grpclb
  implementation library.java.grpc_netty
  implementation library.java.grpc_netty_shaded
  implementation library.java.grpc_stub
  implementation library.java.grpc_google_cloud_pubsub_v1
  implementation library.java.grpc_google_cloud_pubsublite_v1
  implementation library.java.guava
  implementation library.java.hamcrest_core
  implementation library.java.junit
  implementation library.java.jackson_databind
  implementation library.java.joda_time
  implementation library.java.netty_handler
  implementation library.java.netty_tcnative_boringssl_static
  implementation library.java.proto_google_cloud_bigquery_storage_v1beta1
  implementation library.java.proto_google_cloud_bigtable_v2
  implementation library.java.proto_google_cloud_datastore_v1
  implementation library.java.proto_google_cloud_pubsub_v1
  implementation library.java.proto_google_cloud_pubsublite_v1
  implementation library.java.proto_google_cloud_spanner_admin_database_v1
  implementation library.java.proto_google_common_protos
  implementation library.java.protobuf_java
  implementation library.java.slf4j_api
  testImplementation project(path: ":sdks:java:core", configuration: "shadowTest")
  testImplementation project(path: ":sdks:java:extensions:google-cloud-platform-core", configuration: "testRuntime")
  testImplementation project(path: ":runners:direct-java", configuration: "shadow")
  testImplementation project(path: ":sdks:java:io:common", configuration: "testRuntime")
  testImplementation project(path: ":sdks:java:testing:test-utils", configuration: "testRuntime")
  // For testing Cross-language transforms
  testImplementation project(":runners:core-construction-java")
  testImplementation library.java.hamcrest_library
  testImplementation library.java.mockito_core
  testImplementation library.java.powermock
  testImplementation library.java.powermock_mockito
  testRuntimeOnly library.java.slf4j_jdk14
}


/**
 * These are integration tests with the real Pubsub service and the DirectRunner.
 */
task integrationTest(type: Test, dependsOn: processTestResources) {
  group = "Verification"
  def gcpProject = project.findProperty('gcpProject') ?: 'apache-beam-testing'
  def gcpTempRoot = project.findProperty('gcpTempRoot') ?: 'gs://temp-storage-for-end-to-end-tests'
  systemProperty "beamTestPipelineOptions", JsonOutput.toJson([
          "--runner=DirectRunner",
          "--project=${gcpProject}",
          "--tempRoot=${gcpTempRoot}",
  ])

  // Disable Gradle cache: these ITs interact with live service that should always be considered "out of date"
  outputs.upToDateWhen { false }

  include '**/*IT.class'
  exclude '**/BigQueryIOReadIT.class'
  exclude '**/BigQueryIOStorageQueryIT.class'
  exclude '**/BigQueryIOStorageReadIT.class'
  exclude '**/BigQueryIOStorageReadTableRowIT.class'
  exclude '**/BigQueryToTableIT.class'
  maxParallelForks 4
  classpath = sourceSets.test.runtimeClasspath
  testClassesDirs = sourceSets.test.output.classesDirs

  useJUnit {
    excludeCategories "org.apache.beam.sdk.testing.UsesKms"
  }
}

task integrationTestKms(type: Test) {
  group = "Verification"
  def gcpProject = project.findProperty('gcpProject') ?: 'apache-beam-testing'
  def gcpTempRoot = project.findProperty('gcpTempRootKms') ?: 'gs://temp-storage-for-end-to-end-tests-cmek'
  def dataflowKmsKey = project.findProperty('dataflowKmsKey') ?: "projects/apache-beam-testing/locations/global/keyRings/beam-it/cryptoKeys/test"
  systemProperty "beamTestPipelineOptions", JsonOutput.toJson([
          "--runner=DirectRunner",
          "--project=${gcpProject}",
          "--tempRoot=${gcpTempRoot}",
          "--dataflowKmsKey=${dataflowKmsKey}",
  ])

  // Disable Gradle cache: these ITs interact with live service that should always be considered "out of date"
  outputs.upToDateWhen { false }

  include '**/*IT.class'
  maxParallelForks 4
  classpath = sourceSets.test.runtimeClasspath
  testClassesDirs = sourceSets.test.output.classesDirs
  useJUnit {
    includeCategories "org.apache.beam.sdk.testing.UsesKms"
  }
}

task postCommit {
  group = "Verification"
  description = "Integration tests of GCP connectors using the DirectRunner."
  dependsOn integrationTest
  dependsOn integrationTestKms
}
