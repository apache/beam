/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * License); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an AS IS BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins { id 'org.apache.beam.module' }
applyJavaNature(automaticModuleName: 'org.apache.beam.sdk.io.hadoop.common')

description = "Apache Beam :: SDKs :: Java :: IO :: Hadoop Common"
ext.summary = "Library to add shared Hadoop classes among Beam IOs."

configurations {
  hadoopVersion285
}

def version285 = '2.8.5'

dependencies {
  compile project(path: ":sdks:java:core", configuration: "shadow")
  provided library.java.hadoop_client
  provided library.java.hadoop_common
  provided library.java.hadoop_mapreduce_client_core
  testCompile library.java.hamcrest_core
  testCompile library.java.hamcrest_library
  testCompile library.java.junit
  hadoopVersion285 "org.apache.hadoop:hadoop-client:$version285"
  hadoopVersion285 "org.apache.hadoop:hadoop-common:$version285"
  hadoopVersion285 "org.apache.hadoop:hadoop-mapreduce-client-core:$version285"
}

configurations.hadoopVersion285 {
  resolutionStrategy {
    force "org.apache.hadoop:hadoop-client:$version285"
    force "org.apache.hadoop:hadoop-common:$version285"
    force "org.apache.hadoop:hadoop-mapreduce-client-core:$version285"
  }
}

task hadoopVersion285Test(type: Test) {
  description = "Runs Hadoop common tests with Hadoop version $version285"
  include '**/*Test.class'
}
