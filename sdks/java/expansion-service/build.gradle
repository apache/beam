/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * License); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an AS IS BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'org.apache.beam.module'
applyJavaNature(
  automaticModuleName: 'org.apache.beam.sdk.expansion.service',
)


description = "Apache Beam :: SDKs :: Java :: Expansion Service"
ext.summary = """Contains code that can be used to run an expansion service."""


// Exclude tests that need a runner
test {
  systemProperty "beamUseDummyRunner", "true"
  useJUnit {
    excludeCategories "org.apache.beam.sdk.testing.NeedsRunner"
  }
}

dependencies {
  implementation project(path: ":model:pipeline", configuration: "shadow")
  implementation project(path: ":model:fn-execution", configuration: "shadow")
  implementation project(path: ":model:job-management", configuration: "shadow")
  implementation project(path: ":sdks:java:core", configuration: "shadow")
  implementation project(path: ":runners:java-fn-execution")
  implementation project(path: ":sdks:java:harness")
  implementation library.java.snake_yaml
  permitUnusedDeclared project(path: ":model:fn-execution")
  implementation library.java.vendored_grpc_1_60_1
  implementation library.java.vendored_guava_32_1_2_jre
  implementation library.java.slf4j_api
  runtimeOnly library.java.slf4j_jdk14
  testImplementation library.java.junit
  testImplementation library.java.hamcrest
}

task runExpansionService (type: JavaExec) {
  mainClass = "org.apache.beam.sdk.expansion.service.ExpansionService"
  classpath = sourceSets.main.runtimeClasspath
  args = [project.findProperty("constructionService.port") ?: "8097"]
}
