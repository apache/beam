apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
  compileOnly "com.google.guava:guava:${guavaVersion}"
}

// ~ remove default archives

configurations.runtime.artifacts.with { archives ->
  archives.each {
    archives.remove(it)
  }
}

// ~ disable jar task

jar.enabled = false

// ~ create shadow jar

shadowJar {
  relocate 'com.google.common', 'cz.seznam.euphoria.shadow.com.google.common'
  relocate 'com.google.thirdparty', 'cz.seznam.euphoria.shadow.com.google.thirdparty'
  configurations = [project.configurations.compileOnly]
}

jar.dependsOn shadowJar

publishing {
  publications {
    shadow(MavenPublication) { publication ->
      publication.artifact(shadowJar) {
        classifier = null
      }
    }
  }
}
