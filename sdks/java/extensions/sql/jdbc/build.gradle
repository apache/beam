/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * License); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an AS IS BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: org.apache.beam.gradle.BeamModulePlugin
applyJavaNature(failOnWarning: true, enableSpotless: true, testShadowJar: true, shadowClosure: {})

configurations {
  integrationTest
}

dependencies {
  compile project(path: ":beam-sdks-java-extensions-sql", configuration: "shadow")
  compile "jline:jline:2.14.6"
  compile "sqlline:sqlline:1.3.0"
  compile library.java.slf4j_jdk14
  compileOnly library.java.findbugs_annotations
  testCompileOnly library.java.findbugs_annotations
  testCompile library.java.junit
  // Depending on outputs so integrationTest can run with only test dependencies.
  // This enables us to test the JDBC jar being loaded on a custom classloader.
  integrationTest sourceSets.test.output
  integrationTest sourceSets.main.output
  integrationTest library.java.junit
}

shadowJar {
  manifest {
    attributes "Main-Class": "org.apache.beam.sdk.extensions.sql.jdbc.BeamSqlLine"
  }
}

test {
  doFirst {
    // Assert everything is in the output or test jar
    assert classpath.size() == 2
  }
}

task integrationTest(type: Test, dependsOn: ":beam-sdks-java-extensions-sql-jdbc:shadowJar") {
  group = "Verification"

  // Disable Gradle cache (it should not be used because the IT's won't run).
  outputs.upToDateWhen { false }

  systemProperty "driver.jar", configurations.shadowTestRuntimeClasspath[1]

  include '**/*IT.class'
  classpath = configurations.integrationTest
  useJUnit { }
}
