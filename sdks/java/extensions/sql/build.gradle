/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * License); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an AS IS BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: project(":").file("build_rules.gradle")
applyJavaNature()
apply plugin: 'ca.coglinc.javacc'

description = "Apache Beam :: SDKs :: Java :: Extensions :: SQL"

test {
  jvmArgs "-da"
}

configurations {
  // Create an fmppTask configuration representing the dependencies
  // required to define and execute the Ant FMPP task.
  // TODO: Migrate to a FMPP plugin once one exists
  fmppTask
  fmppTemplates
}


def calcite_version = "1.13.0"
def avatica_version = "1.10.0"

dependencies {
  fmppTask "net.sourceforge.fmpp:fmpp:0.9.15"
  fmppTask "org.freemarker:freemarker:2.3.25-incubating"
  fmppTemplates "org.apache.calcite:calcite-core:$calcite_version"
  compile library.java.guava
  compile "org.apache.calcite:calcite-core:$calcite_version"
  compile "org.apache.calcite:calcite-linq4j:$calcite_version"
  compile "org.apache.calcite.avatica:avatica-core:$avatica_version"
  compile "com.alibaba:fastjson:1.2.12"
  compileOnly library.java.findbugs_jsr305
  shadow project(path: ":beam-sdks-parent:beam-sdks-java-parent:beam-sdks-java-core", configuration: "shadow")
  shadow project(path: ":beam-sdks-parent:beam-sdks-java-parent:beam-sdks-java-extensions-parent:beam-sdks-java-extensions-join-library", configuration: "shadow")
  shadow library.java.slf4j_api
  shadow library.java.commons_csv
  shadow library.java.joda_time
  shadow project(path: ":beam-runners-parent:beam-runners-direct-java", configuration: "shadow")
  provided project(path: ":beam-sdks-parent:beam-sdks-java-parent:beam-sdks-java-io-parent:beam-sdks-java-io-kafka", configuration: "shadow")
  provided library.java.kafka_clients
  testCompile library.java.slf4j_jdk14
  testCompile library.java.junit
  testCompile library.java.hamcrest_core
  testCompile library.java.mockito_core
}

// Copy Caclcite templates and our own template into the build directory
// so we have one location for the FMPP task to parse.
task copyFmppTemplatesFromSrc(type: Copy) {
  from "src/main/codegen"
  into "build/templates-fmpp/codegen"
}
task copyFmppTemplatesFromCalciteCore(type: Copy) {
  dependsOn configurations.fmppTemplates
  File calciteCoreJar = files(configurations.fmppTemplates.files).filter {
    it.name.startsWith("calcite-core")
  }.singleFile
  from zipTree(calciteCoreJar)
  include "**/Parser.jj"
  into "${project.buildDir}/templates-fmpp"
}

// Generate the FMPP sources from the FMPP templates.
def generateFmppOutputDir = "${project.buildDir}/generated-fmpp"
task generateFmppSources {
  dependsOn configurations.fmppTask
  dependsOn copyFmppTemplatesFromSrc
  dependsOn copyFmppTemplatesFromCalciteCore
  doLast {
    ant.taskdef(name: "fmpp", classname: "fmpp.tools.AntTask", classpath: configurations.fmppTask.asPath)
    ant.fmpp(configuration: "src/main/codegen/config.fmpp", sourceRoot: "${project.buildDir}/templates-fmpp/codegen/templates", outputRoot: generateFmppOutputDir)
  }
}

compileJavacc {
  dependsOn generateFmppSources
  inputDirectory = file(generateFmppOutputDir)
  arguments = [grammar_encoding: "UTF-8", static: "false", lookahead: "2"]
}

shadowJar {
  dependencies {
    include(dependency("org.apache.calcite:.*"))
    include(dependency("org.apache.calcite.avatica:.*"))
    include(dependency("org.codehaus.janino:.*"))
    include(dependency(library.java.protobuf_java))
    include(dependency(library.java.protobuf_java_util))
  }
  relocate "com.google.protobuf", "org.apache.beam." + project.name.replace("-", ".") + ".repackaged.com.google.protobuf"
  relocate "org.apache.calcite", "org.apache.beam." + project.name.replace("-", ".") + ".repackaged.org.apache.calcite"

  // Calcite fails to look up the compiler factory after this relocation if destination is 'org.apache.beam' like above.
  // The factory is looked by class name which stored in the resources.
  // Shading plugin doesn't handle replacing that class name correctly.
  relocate "org.codehaus", "org.apache." + project.name.replace("-", ".") + ".repackaged.org.codehaus"
}


task packageTests(type: Jar) {
  from sourceSets.test.output
  classifier = "tests"
}

artifacts.archives packageTests
