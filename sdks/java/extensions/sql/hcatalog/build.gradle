/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * License); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an AS IS BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins { id 'org.apache.beam.module' }

applyJavaNature(
  automaticModuleName: 'org.apache.beam.sdk.extensions.sql.meta.provider.hcatalog',
  classesTriggerCheckerBugs: [
    'HCatalogTable': 'TODO: file a bug report',
  ]
)

def hive_version = "3.1.3"
def netty_version = "4.1.51.Final"

configurations.all {
  resolutionStrategy {
    // Pin log4j as workaround for CVE-2021-44228
    // HIVE-25804 should address this upstream, but only in 4.0
    // TODO(BEAM-9351): Upgrade Hive and remove this pin
    def log4j_version = "2.17.1"
    force "org.apache.logging.log4j:log4j-api:${log4j_version}"
    force "org.apache.logging.log4j:log4j-core:${log4j_version}"
    force "org.apache.logging.log4j:log4j-slf4j-impl:${log4j_version}"
    force "org.apache.logging.log4j:log4j-1.2-api:${log4j_version}"
    force "org.apache.logging.log4j:log4j-web:${log4j_version}"
  }
}

/*
 * We need to rely on manually specifying these evaluationDependsOn to ensure that
 * the following projects are evaluated before we evaluate this project. This is because
 * we are attempting to reference the "sourceSets.test.output" directly.
 * TODO: use testTextures feature which is introduced in Gradle 5.6 instead of
 * the test outputs directly.
 */
evaluationDependsOn(":sdks:java:io:hcatalog")

dependencies {
  provided project(":sdks:java:extensions:sql")
  provided project(":sdks:java:io:hcatalog")

  testImplementation project(":sdks:java:io:hcatalog").sourceSets.test.output
  // Needed for HCatalogTableProvider tests,
  // they use HCat* types
  testCompile "io.netty:netty-all:$netty_version"
  testCompile("org.apache.hive.hcatalog:hive-hcatalog-core:$hive_version") {
    // Hive brings full Calcite 1.6 + Avatica with JDBC driver which
    // gets registered and gets started instead of ours
    exclude group: "org.apache.calcite", module:"calcite-avatica"
  }
}
