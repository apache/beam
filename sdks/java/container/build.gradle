/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * License); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an AS IS BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: project(":").file("build_rules.gradle")
applyGoNature()
applyDockerNature()

description = "Apache Beam :: SDKs :: Java :: Container"

// Figure out why the golang plugin does not add a build dependency between projects.
// Without the line below, we get spurious errors about not being able to resolve
// "./github.com/apache/beam/sdks/go"
resolveBuildDependencies.dependsOn ":beam-sdks-go:build"

configurations {
  dockerDependency
}

dependencies {
  golang {
    // TODO(herohde): use "./" prefix to prevent gogradle use base github path, for now.
    // TODO(herohde): get the pkg subdirectory only, if possible. We spend mins pulling cmd/beamctl deps.
    build name: './github.com/apache/beam/sdks/go', dir: project(':beam-sdks-go').projectDir
    test name: './github.com/apache/beam/sdks/go', dir: project(':beam-sdks-go').projectDir
  }
  dockerDependency library.java.slf4j_api
  dockerDependency library.java.slf4j_jdk14
  dockerDependency project(path: ":beam-sdks-java-harness", configuration: "shadow")
}

task copyDockerfileDependencies(type: Copy) {
  from configurations.dockerDependency
  rename "slf4j-api.*", "slf4j-api.jar"
  rename "slf4j-jdk14.*", "slf4j-jdk14.jar"
  rename "harness.*\\.jar", "beam-sdks-java-harness.jar"
  into "build/target"
}

golang {
  packagePath = 'github.com/apache/beam/sdks/java/boot'
  build {
    // TODO(herohde): build local platform + linux-amd64, if possible.
    targetPlatform = ['linux-amd64']
    outputLocation = './build/target/${GOOS}_${GOARCH}/boot'
  }
}

docker {
  name containerImageName(name: "java")
  files "./build/"
}
// Ensure that we build the required resources and copy and file dependencies from related projects
dockerPrepare.dependsOn build
dockerPrepare.dependsOn copyDockerfileDependencies
