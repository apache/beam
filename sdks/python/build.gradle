/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * License); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an AS IS BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Define common lifecycle tasks and artifact types
apply plugin: "base"

// For some reason base doesn't define a test task  so we define it below and make
// check depend on it. This makes the Python project similar to the task layout like
// Java projects, see https://docs.gradle.org/4.2.1/userguide/img/javaPluginTasks.png
task test {}
check.dependsOn test

task setupTest {
  doLast {
    exec {
      executable 'sh'
      args '-c', 'which tox || pip install --user --upgrade tox'
    }
  }
}

task sdist {
  doLast {
    exec {
      commandLine 'python', 'setup.py', 'sdist', '--formats', 'zip,gztar', '--dist-dir', project.buildDir
    }
  }
}

task cleanPython {
  doLast {
    exec {
      commandLine 'python', 'setup.py', 'clean'
    }
  }
}
clean.dependsOn cleanPython

task buildPython {
  doLast {
    println 'Building Python Dependencies'
    exec {
      commandLine 'python', 'setup.py', 'build', '--build-base', project.buildDir
    }
  }
}
build.dependsOn buildPython

task lint (dependsOn: 'setupTest') {
  doLast {
    exec {
      commandLine 'tox', '-e', 'py27-lint', '-c', 'tox.ini'
    }
  }
}
check.dependsOn lint

// Ensure that testCython runs exclusively to other tests.
task testCython (dependsOn: ['setupTest', 'testPython', 'testGcp']) {
  doLast {
    exec {
      commandLine 'tox', '-e', 'py27-cython', '-c', 'tox.ini'
    }
  }
}
test.dependsOn testCython

task testGcp (dependsOn: 'setupTest') {
  doLast {
    exec {
      commandLine 'tox', '-e', 'py27-gcp', '-c', 'tox.ini'
    }
  }
}
test.dependsOn testGcp

task testPython (dependsOn: 'setupTest') {
  doLast {
         exec {
           commandLine 'tox', '-e', 'ALL', '-c', 'tox.ini'
         }
  }
}
test.dependsOn testPython
