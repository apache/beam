 /*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * License); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an AS IS BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: org.apache.beam.gradle.BeamModulePlugin
applyPythonNature()

dependencies {
  distTarBall project(path: ":sdks:python", configuration: "distTarBall")
}

pythonVersion = '2.7'

description = "Apache Beam :: SDKs :: Python :: Chicago Taxi"

task installChicagoTaxiDependencies(dependsOn: installGcpTest) {
  doLast {
    exec {
      executable 'sh'
      args '-c', ". ${envdir}/bin/activate && pip install -r requirements.txt"
    }
  }
}

task runChicagoTaxi(type: Exec, dependsOn: installChicagoTaxiDependencies) {
  def gcsRoot = findProperty('gcsRoot')
  def runner = findProperty('runner')
  def sdkLocation = "${files(configurations.distTarBall.files).singleFile}"
  def jobEndpoint = findProperty('jobEndpoint')
  def environmentConfig = findProperty('environmentConfig')
  def environmentType = findProperty('environmentType')
  def publishToBigQuery = findProperty('publishToBigQuery') ?: "false"
  def metricsTableSuffix = findProperty('metricsTableSuffix')
  def parallelism = findProperty('parallelism')

  if (runner == 'DataflowRunner' || runner == 'TestDataflowRunner') {
    dependsOn ':sdks:python:sdist'
  }

  environment([
    'GCS_BUCKET'          : gcsRoot,
    'RUNNER'              : runner,
    'SDK_LOCATION'        : sdkLocation,
    'JOB_ENDPOINT'        : jobEndpoint,
    'ENVIRONMENT_CONFIG'  : environmentConfig,
    'ENVIRONMENT_TYPE'    : environmentType,
    'PARALLELISM'         : parallelism,
    'PUBLISH_TO_BIG_QUERY': publishToBigQuery,
    'METRICS_TABLE_SUFFIX': metricsTableSuffix,
  ])

  commandLine 'sh', '-c', ". ${envdir}/bin/activate && ./run_chicago.sh"
}
