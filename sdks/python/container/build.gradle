/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * License); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an AS IS BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins { id 'org.apache.beam.module' }
applyGoNature()

description = "Apache Beam :: SDKs :: Python :: Container"

// Disable gogradle's dependency resolution so it uses Go modules instead.
installDependencies.enabled = false
resolveBuildDependencies.enabled = false
resolveTestDependencies.enabled = false

configurations {
  sdkSourceTarball
  sdkHarnessLauncher
}

dependencies {
  golang {
    // TODO(herohde): use "./" prefix to prevent gogradle use base github path, for now.
    // TODO(herohde): get the pkg subdirectory only, if possible. We spend mins pulling cmd/beamctl deps.
    build name: './github.com/apache/beam/sdks/go', dir: project(':sdks:go').projectDir
    test name: './github.com/apache/beam/sdks/go', dir: project(':sdks:go').projectDir
  }
  sdkSourceTarball project(path: ":sdks:python", configuration: "distTarBall")
}

golang {
  packagePath = 'github.com/apache/beam/sdks/v2/python/container'

  // Set -modcacherw on common tasks so module cache can be deleted by goBuild.
  goTest {
    environment 'GOFLAGS', '-modcacherw'
  }
  goVet {
    environment 'GOFLAGS', '-modcacherw'
  }
  gofmt {
    environment 'GOFLAGS', '-modcacherw'
  }
  goBuild {
    environment 'GOFLAGS', '-modcacherw'

    // Build for linux and mac.
    targetPlatform = ['linux-amd64', 'darwin-amd64']
    outputLocation = './build/target/launcher/${GOOS}_${GOARCH}/boot'
  }
}

task buildAll {
  dependsOn ':sdks:python:container:py36:docker'
  dependsOn ':sdks:python:container:py37:docker'
  dependsOn ':sdks:python:container:py38:docker'
}

task pushAll {
  dependsOn ':sdks:python:container:py36:dockerPush'
  dependsOn ':sdks:python:container:py37:dockerPush'
  dependsOn ':sdks:python:container:py38:dockerPush'
}

task generatePythonRequirementsAll {
  dependsOn ':sdks:python:container:py36:generatePythonRequirements'
  dependsOn ':sdks:python:container:py37:generatePythonRequirements'
  dependsOn ':sdks:python:container:py38:generatePythonRequirements'
}

artifacts {
  sdkHarnessLauncher file: file('./build/target/launcher'), builtBy: goBuild
}
