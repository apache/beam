/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * License); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an AS IS BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'base'
  id 'org.apache.beam.module'
}
applyDockerNature()

description = "Apache Beam :: SDKs :: Python :: Container :: Python 37 Container"

configurations {
  sdkSourceTarball
  sdkHarnessLauncher
}

dependencies {
  sdkSourceTarball project(path: ":sdks:python", configuration: "distTarBall")
  sdkHarnessLauncher project(path: ":sdks:python:container", configuration: "sdkHarnessLauncher")
}

task copyDockerfileDependencies(type: Copy) {
  from configurations.sdkSourceTarball
  from file("../base_image_requirements.txt")
  into "build/target"
  if(configurations.sdkSourceTarball.isEmpty()) {
      throw new StopExecutionException();
  }
}

task copyLauncherDependencies(type: Copy) {
  from configurations.sdkHarnessLauncher
  into "build/target/launcher"
  if(configurations.sdkHarnessLauncher.isEmpty()) {
      throw new StopExecutionException();
  }
}

docker {
  name containerImageName(
          name: "python3.7_sdk",
          root: project.rootProject.hasProperty(["docker-repository-root"]) ?
                  project.rootProject["docker-repository-root"] : "apachebeam",
          tag: project.rootProject.hasProperty(["docker-tag"]) ?
                  project.rootProject["docker-tag"] : project.sdk_version)
  files "../Dockerfile", "./build"
  buildArgs(['py_version': "3.7"])
}


dockerPrepare.dependsOn copyLauncherDependencies
dockerPrepare.dependsOn copyDockerfileDependencies
